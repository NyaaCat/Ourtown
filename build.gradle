apply plugin: "java-library"
apply plugin: "maven-publish"

ext.majorVersion = 6
ext.minorVersion = 4
ext.minecraftVersion = "1.14.4"
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Suppiled by Jenkins
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x"                : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"

// Version used for distribution. Different from maven repo
group = "cat.nyaa"
archivesBaseName = "Ourtown-mc$minecraftVersion"
version = "$majorVersion.$minorVersion.$buildNumber"

repositories {
    jcenter()
    maven { name 'Spigot';      url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name 'Sonatype';    url 'https://oss.sonatype.org/content/groups/public' }
    maven { name 'sk89q';       url 'http://maven.sk89q.com/artifactory/repo/' }
    maven { name 'EssentialsX'; url 'https://ci.ender.zone/plugin/repository/everything' }
    maven { name 'vault-repo';  url 'https://jitpack.io' }
    maven { name 'NyaaCat';     url 'https://ci.nyaacat.com/maven/' }
}

dependencies {
    // spigot
    compileOnly "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"

    // other nyaa plugins
    if (gradle.hasProperty("useLocalDependencies") && gradle.useLocalDependencies) {
        compileOnly project(":NyaaCore")
    } else {
        compileOnly('cat.nyaa:nyaacore:6.4-SNAPSHOT') { transitive = false }
    }

    // 3rd party plugins
    compileOnly('net.ess3:EssentialsX:2.17.0') { transitive = false }
    compileOnly 'javax.persistence:javax.persistence-api:2.2' // NyaaCore shadows this

    // annotation processor
    compileOnly 'org.librazy:NyaaUtilsLangChecker:2.0-SNAPSHOT'
    annotationProcessor 'org.librazy:NyaaUtilsLangChecker:2.0-SNAPSHOT'
    annotationProcessor "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
}

// TODO: langcheck temporarily disabled
// String nyaaCoreLangDir = System.getenv("NYAACORE_LANG_DIR")
// compileJava {
//     options.compilerArgs += ["-Xplugin:NyaaUtilsLangAnnotationProcessor", "-Xlint:deprecation,unchecked,rawtypes"]
//     if (nyaaCoreLangDir != null) {
//         options.compilerArgs += ["-ALANG_DIR_ADDITIONAL_PATH=" + nyaaCoreLangDir]
//     }
// }

// publications
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}
processResources { // modify version string
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            groupId "cat.nyaa"
            artifactId "ourtown"
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "${mavenDirectory}"
        }
    }
}


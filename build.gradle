apply plugin: "java-library"
apply plugin: "maven-publish"

ext.majorVersion = 8
ext.minorVersion = 6
ext.minecraftVersion = "1.21.8"
sourceCompatibility = 21
targetCompatibility = 21

// Suppiled by Jenkins
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x" : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"

// Version used for distribution. Different from maven repo
group = "cat.nyaa"
archivesBaseName = "Ourtown"
version = "$majorVersion.$minorVersion"

repositories {
    mavenCentral()
    maven { name 'Spigot'; url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name 'Sonatype'; url 'https://oss.sonatype.org/content/groups/public' }
    maven { name 'enginehub'; url 'https://maven.enginehub.org/repo/' }
    maven { name "essentialsx-releases"; url "https://repo.essentialsx.net/releases/" }
    maven { name "papermc"; url "https://papermc.io/repo/repository/maven-public/" }
    maven { name 'jitpack'; url 'https://jitpack.io' }
    maven { name 'NyaaCat'; url 'https://ci.nyaacat.com/maven/' }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:22.0.0'

    // spigot
    // compileOnly "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
    compileOnly "io.papermc.paper:paper-api:$minecraftVersion-R0.1-SNAPSHOT"
    // other nyaa plugins
    compileOnly('cat.nyaa:nyaacore:9.10')
    compileOnly('cat.nyaa:ecore:0.3.5')


    // 3rd party plugins
    compileOnly('net.essentialsx:EssentialsX:2.21.2')

    // // annotation processor
    // annotationProcessor "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
}

// publications
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}
processResources { // modify version string
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            groupId "cat.nyaa"
            artifactId "ourtown"
            version "$majorVersion.$minorVersion"
            from components.java
        }
    }
    repositories {
        maven {
            url "${mavenDirectory}"
        }
    }
}

